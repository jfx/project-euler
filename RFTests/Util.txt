*** Keywords ***
I check in local the resolve CLI command
    [Arguments]    ${id}    ${title}    ${result}    @{description}
    ${output} =    When I run local CLI command to resolve problem #    ${id}
    Then the title on stdout should be    ${output}    ${title}
    And the description on stdout should be    ${output}    @{description}
    And the result on stdout should be    ${output}    ${result}

I check with docker the resolve CLI command
    [Arguments]    ${id}    ${title}    ${result}    @{description}
    ${output} =    When I run docker CLI command to resolve problem #    ${id}
    Then the title on stdout should be    ${output}    ${title}
    And the description on stdout should be    ${output}    @{description}
    And the result on stdout should be    ${output}    ${result}

I check the resolve API request
    [Arguments]    ${url}    ${id}    ${title}    ${result}    @{description}
    ${resp}=    When I request successfully the API resolve operation    http://${url}    ${id}
    Then the answer of the API resolve operation should be    ${resp}    ${title}    ${result}    @{description}

I run local CLI command to resolve problem #
    [Arguments]    ${num}
    ${output} =    Run Process    ${PE_DIR}/bin/pe    resolve    ${num}
    Should Be Equal As Integers    ${output.rc}    0
    [Return]    ${output}

I run docker CLI command to resolve problem #
    [Arguments]    ${num}
    ${output} =    Run Process    docker    rm    ${CONTAINER_NAME}
    ${output} =    Run Process    docker    run    --name    ${CONTAINER_NAME}    -d
    ...    ${IMAGE_NAME}    /app/bin/pe    resolve    ${num}
    Should Be Equal As Integers    ${output.rc}    0
    Sleep    ${WAIT_DOCKER_FLUSH}
    ${output} =    Run Process    docker    logs    ${CONTAINER_NAME}
    Should Be Equal As Integers    ${output.rc}    0
    [Return]    ${output}

The title on stdout should be
    [Arguments]    ${output}    ${title}
    Should Contain    ${output.stdout}    ${title}

The description on stdout should be
    [Arguments]    ${output}    @{description}
    Should Contain    ${output.stdout}    @{description}

The result on stdout should be
    [Arguments]    ${output}    ${result}
    Should Contain    ${output.stdout}    ${result}

I start built-in PHP server
    ${handle} =    Start Process    php -S localhost:${BUILTIN_SERVER_PORT} -t ${PE_DIR}/web    cwd=${PE_DIR}    shell=True
    Process Should Be Running    handle=${handle}
    sleep    ${WAIT_BUILTIN_SERVER_START}

I request successfully the API resolve operation
    [Arguments]    ${url}    ${id}
    ${resp}=    I request the API resolve operation    ${url}    ${id}
    Should Be Equal As Strings    ${resp.status_code}    200
    [Return]    ${resp}

I request the API resolve operation
    [Arguments]    ${url}    ${id}
    Create Session    pe    ${url}
    ${resp}=    Get Request    pe    /resolve/${id}
    [Return]    ${resp}

The answer of the API resolve operation should be
    [Arguments]    ${resp}    ${title}    ${result}    @{description}
    Dictionary Should Contain Key    ${resp.json()}    id
    Dictionary Should Contain Key    ${resp.json()}    title
    Dictionary Should Contain Value    ${resp.json()}    ${title}
    Dictionary Should Contain Key    ${resp.json()}    description
    Dictionary Should Contain Key    ${resp.json()}    solution
    Dictionary Should Contain Value    ${resp.json()}    ${result}
    Dictionary Should Contain Key    ${resp.json()}    time
    Dictionary Should Contain Key    ${resp.json()}    roundTime
